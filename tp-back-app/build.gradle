plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.12'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.asciidoctor.jvm.convert" version "3.3.2" // Asciidoctor 플러그인 적용
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '17'
    }

    group = 'com.trip.penguin'
    version = '1.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly('org.projectlombok:lombok:1.18.28')
        annotationProcessor("org.projectlombok:lombok:1.18.28")
        testCompileOnly 'org.projectlombok:lombok:1.18.28'
    }
}

project('application:tp-back-internal-app') {

    configurations {
        asciidoctorExt
    }

    ext {
        snippetsDir = file('build/generated-snippets')
    }

    test {
        outputs.dir snippetsDir
    }

    asciidoctor {
        inputs.dir snippetsDir
        configurations 'asciidoctorExt'
        dependsOn test
    }

    bootJar {
        enabled = true
        dependsOn asciidoctor
        from ("${asciidoctor.outputDir}/html5") {
            into 'static/docs'
        }
    }

    jar {
        enabled = false
    }

    dependencies {
        implementation project(':domain:tp-rdb-domain')
        testCompileOnly project(':domain:tp-rdb-domain')

        testCompileOnly project(':system:tp-web-system')
        implementation project(':system:tp-web-system')

        // https://mvnrepository.com/artifact/mysql/mysql-connector-java
        implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
        implementation 'org.springframework.boot:spring-boot-starter-test'

        asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.0'
        // https://mvnrepository.com/artifact/org.springframework.restdocs/spring-restdocs-mockmvc
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.0'


        implementation 'org.springframework.boot:spring-boot-starter-web'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }
}


project('application:tp-batch-app') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}


project('domain:tp-rdb-domain') {
    dependencies {
        implementation project(':core:tp-base-core')
        testCompileOnly project(':core:tp-base-core')

        // https://mvnrepository.com/artifact/mysql/mysql-connector-java
        implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
        implementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project('system:tp-web-system') {
    dependencies {
        implementation project(':core:tp-base-core')
        testCompileOnly project(':core:tp-base-core')

        implementation 'org.springframework.boot:spring-boot-starter-web'
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
        implementation 'org.springframework.boot:spring-boot-starter-security:3.1.5'
    }
}

project('core:tp-base-core') {
    dependencies {}
}